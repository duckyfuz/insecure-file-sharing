locals {
  acm_certificate_record       = tolist(aws_acm_certificate.subdomain_cert.domain_validation_options)[0]
  acm_certificate_record_name  = replace(local.acm_certificate_record.resource_record_name, ".${var.domain}.", "")
  acm_certificate_record_type  = local.acm_certificate_record.resource_record_type
  acm_certificate_record_value = substr(local.acm_certificate_record.resource_record_value, 0, length(local.acm_certificate_record.resource_record_value) - 1)
}

resource "cloudflare_record" "acm_certificate_cname" {
  zone_id = var.cloudflare_zone_id
  name    = local.acm_certificate_record_name
  type    = local.acm_certificate_record_type
  content = local.acm_certificate_record_value
  ttl     = 3600
  proxied = false
}

resource "cloudflare_record" "cloudfront_to_subdomain_cname" {
  zone_id = var.cloudflare_zone_id
  comment = "GENERATED BY TERRAFORM - for cloudfront record"
  name    = var.subdomain
  type    = "CNAME"
  content = aws_cloudfront_distribution.s3_distribution.domain_name
  ttl     = 1
  proxied = true
}

resource "cloudflare_turnstile_widget" "ifs_widget" {
  account_id = var.cloudflare_account_id
  name       = "${var.project_name} Turnstile Widget"
  domains    = [local.fqdn, "localhost"]
  mode       = "managed"
}
