locals {
  # Use try() to safely handle the case where subdomain_cert doesn't exist (preview mode, count=0).
  acm_dvo                      = try(tolist(aws_acm_certificate.subdomain_cert[0].domain_validation_options)[0], null)
  acm_certificate_record_name  = try(replace(local.acm_dvo.resource_record_name, ".${var.domain}.", ""), "")
  acm_certificate_record_type  = try(local.acm_dvo.resource_record_type, "")
  acm_certificate_record_value = try(substr(local.acm_dvo.resource_record_value, 0, length(local.acm_dvo.resource_record_value) - 1), "")
}

# DNS validation record for ACM cert — production only
resource "cloudflare_record" "acm_certificate_cname" {
  count   = var.is_preview ? 0 : 1
  zone_id = var.cloudflare_zone_id
  name    = local.acm_certificate_record_name
  type    = local.acm_certificate_record_type
  content = local.acm_certificate_record_value
  ttl     = 3600
  proxied = false
}

# CloudFront CNAME in Cloudflare — production only
resource "cloudflare_record" "cloudfront_to_subdomain_cname" {
  count   = var.is_preview ? 0 : 1
  zone_id = var.cloudflare_zone_id
  comment = "GENERATED BY TERRAFORM - for cloudfront record"
  name    = var.subdomain
  type    = "CNAME"
  content = aws_cloudfront_distribution.s3_distribution.domain_name
  ttl     = 1
  proxied = true
}

# Vercel landing page CNAME — production only
resource "cloudflare_record" "vercel_landing_cname" {
  count   = (!var.is_preview && var.vercel_cname != "") ? 1 : 0
  zone_id = var.cloudflare_zone_id
  comment = "GENERATED BY TERRAFORM - for vercel landing page"
  name    = var.vercel_subdomain
  type    = "CNAME"
  content = var.vercel_cname
  ttl     = 1
  proxied = false
}

# Production Turnstile widget
resource "cloudflare_turnstile_widget" "ifs_widget" {
  count      = var.is_preview ? 0 : 1
  account_id = var.cloudflare_account_id
  name       = "${var.project_name} Turnstile Widget"
  domains    = [local.fqdn, "localhost"]
  mode       = "managed"
}

# Preview Turnstile widget — allows *.cloudfront.net for ephemeral PR environments
resource "cloudflare_turnstile_widget" "ifs_widget_preview" {
  count      = var.is_preview ? 1 : 0
  account_id = var.cloudflare_account_id
  name       = "${local.resource_name} Preview Turnstile Widget"
  domains    = ["cloudfront.net", "localhost"]
  mode       = "managed"
}
